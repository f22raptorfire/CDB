options {
    IGNORE_CASE=true;
    JJTREE_OUTPUT_DIRECTORY="wci/frontend";
    NODE_EXTENDS="wci.intermediate.icodeimpl.ICodeNodeImpl";
    MULTI=true;
    VISITOR=true;
}

PARSER_BEGIN(CdbParser)
package wci.frontend;

import java.util.ArrayList;
import java.io.*;

import wci.intermediate.*;
import wci.intermediate.symtabimpl.*;
import wci.backend.*;
import wci.backend.compiler.*;
import wci.util.*;
import wci.intermediate.typeimpl.TypeFormImpl;

import static wci.intermediate.symtabimpl.SymTabKeyImpl.*;
import static wci.intermediate.symtabimpl.DefinitionImpl.*;
import static wci.intermediate.symtabimpl.RoutineCodeImpl.*;
import static wci.intermediate.symtabimpl.SymTabKeyImpl.*;
import static wci.intermediate.icodeimpl.ICodeKeyImpl.*;

public class CdbParser
{
    private static final String SOURCE_SUFFIX = ".cdb";
    private static final String OUTPUT_SUFFIX = ".j";

    private static SymTabStack symTabStack;
    private static SymTabEntry programId;

    public static void main(String[] args)
        throws Exception
    {
    	// Create and initialize the symbol table stack.
        symTabStack = SymTabFactory.createSymTabStack();
        Predefined.initialize(symTabStack);

        // Process the source file path which ends in .cdb
        // and create the output file path which ends in .j
        String sourceFilePath = args[0];
        int truncatedLength = sourceFilePath.length() - SOURCE_SUFFIX.length();
        int suffixIndex = sourceFilePath.lastIndexOf(SOURCE_SUFFIX);
        String objectFilePath = (suffixIndex == truncatedLength)
            ? sourceFilePath.substring(0, truncatedLength) + OUTPUT_SUFFIX
            : sourceFilePath + OUTPUT_SUFFIX;

		int fileindex = sourceFilePath.lastIndexOf("\\") + 1;
		String programName = sourceFilePath.substring(fileindex, truncatedLength);

        // Parse a CDB program.
        Reader    reader = new FileReader(sourceFilePath);
        CdbParser parser = new CdbParser(reader);
        SimpleNode rootNode = null;
        try        {          rootNode = parser.script(programName);
        } catch (Exception e)        {          System.out.println("Fatal Error!");
          return;
        }

		// Visit the parse tree nodes to decorate them with type information.
        TypeSetterVisitor typeVisitor = new TypeSetterVisitor();
        rootNode.jjtAccept(typeVisitor, symTabStack);
        
        // Print the cross-reference table.
        CrossReferencer crossReferencer = new CrossReferencer();
        crossReferencer.print(symTabStack);
        
		ArrayList<SymTabEntry> entries = ((SymTab)programId.getAttribute(SymTabKeyImpl.ROUTINE_SYMTAB)).sortedEntries();
    	for (SymTabEntry entry : entries) {
    		if (entry.getDefinition() == DefinitionImpl.PROCEDURE) {
				((SimpleNode)entry.getAttribute(ROUTINE_CODE)).jjtAccept(typeVisitor, symTabStack);
    		}
    	}

        // Create and initialize the ICode wrapper for the parse tree.
        ICode iCode = ICodeFactory.createICode();
        iCode.setRoot(rootNode);
        programId.setAttribute(ROUTINE_ICODE, iCode);
        
        // Print the parse tree.
        ParseTreePrinter treePrinter = new ParseTreePrinter(System.out);
        treePrinter.print(symTabStack);

        // Compile the code.
        Backend cg = BackendFactory.createBackend("compile");
        (new File("src/" + programName + ".j")).createNewFile();
        PrintWriter writer = new PrintWriter("src/" + programName + ".j");
        cg.process(iCode, symTabStack, writer);
    }
}
PARSER_END(CdbParser)

SKIP: {
    " " | "\r" | "\t" | "\n" | "\r\n"
}

SKIP:
{
  <SINGLE_LINE_COMMENT : "//" (~["\n","\r"])* ("\r" | "\n" | "\r\n") >
}

//skip multi-line comments
MORE : {
  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_MULTI_LINE_COMMENT>
MORE : {
  < ~[] >
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN : {
  <MULTI_LINE_COMMENT: "*/"> : DEFAULT
}

TOKEN: { /* Reserved words */
    <IF        : "IF">
  | <ELSE      : "ELSE">
  | <TRUE      : "TRUE">
  | <FALSE     : "FALSE">
  | <WHILE     : "WHILE">
  | <FOR       : "FOR">
  | <DO        : "DO">
  | <PRINT     : "PRINT">
  | <PRINTLN   : "PRINTLN">
  | <DATABASE  : "DATABASE">
  | <PROMPT    : "PROMPT">
  | <SHOW      : "SHOW">
  | <ADD       : "ADD">
  | <NEW       : "NEW">
}

TOKEN: /* Special symbols */
{
    <COLON        : ":">
  | <AT           : "@">
  | <DOLLAR 	  : "$">
  | <EQUALS       : "=">
  | <EE           : "==">
  | <NE           : "!=">
  | <LT           : "<">
  | <LE           : "<=">
  | <GT           : ">">
  | <GE           : ">=">
  | <COMMA        : ",">
  | <DOT          : ".">
  | <SEMICOLON    : ";">
  | <PLUS         : "+">
  | <MINUS        : "-">
  | <STAR         : "*">
  | <SLASH        : "/">
  | <PERCENT      : "%">
  | <QUOTE        : "'">
  | <DOUBLE_QUOTE : "\"">
  | <LEFT_BRACKET : "[">
  | <RIGHT_BRACKET: "]">
  | <LEFT_PAREN   : "(">
  | <RIGHT_PAREN  : ")">
  | <LEFT_BRACE   : "{">
  | <RIGHT_BRACE  : "}">   
  | <AND          : "&&">
  | <OR           : "||">
  | <UNDERSCORE   : "_">
}

TOKEN: { /* Identifiers, strings, and numbers */
    <IDENTIFIER : <LETTER>(<LETTER>|<DIGIT>|<UNDERSCORE>)*>
  | <STRING_LIT : <DOUBLE_QUOTE>(~["\""])*<DOUBLE_QUOTE>>

  | <INTEGER    : <UNSIGNED_NUMBER>>
  | <REAL   : <WHOLE><FRACTION><EXPONENT>
             | <WHOLE><FRACTION>
             | <WHOLE><EXPONENT>>

  | <#LETTER : ["a"-"z", "A"-"Z"]>
  | <#DIGIT  : ["0"-"9"]>
  | <#SIGN   : ["+", "-"]>

  | <#UNSIGNED_NUMBER : (<DIGIT>)+>
  | <#WHOLE           : <UNSIGNED_NUMBER>>
  | <#FRACTION        : "." <UNSIGNED_NUMBER>>
  | <#EXPONENT        : "E"(<SIGN>)?<UNSIGNED_NUMBER>>
}

TOKEN: { /*	Error token */
    <EOL     : "\r" | "\n" | "\r\n">
 |  <ERROR   : ~["\r", "\n"]>
}

SimpleNode script(String programName) #COMPOUND : { SimpleNode rootNode; }
{
  {
    programId = symTabStack.enterLocal(programName);
    programId.setDefinition(DefinitionImpl.PROGRAM);
    programId.setAttribute(ROUTINE_SYMTAB, symTabStack.push());
    symTabStack.setProgramId(programId);
  }
  databaseStatement()
  (
    try {
        statement()
    }
    catch (wci.frontend.ParseException ex) {	  handleError(token);
    }
    catch (NullPointerException ex) {	  handleError(token);
    }
  )*
  <EOF>  { return jjtThis; }
}

SimpleNode databaseStatement() #DATABASE : {}
{	<DATABASE> stringConstant() <SEMICOLON>
	{ return jjtThis; }
}

void statement() #void : {}
{
  ( LOOKAHEAD(2)
       assignmentStatement()
    |  routineCall()
    |  declarationStatement()
    |  printStatement()
    |  printlnStatement()
    |  ifStatement()
    |  doLoop()
    |  forLoop()
    |  whileLoop()
    |  promptStatement()
    |  Error()  { handleError(token); }
  )
}

void promptStatement() #void : {}{	<PROMPT> (
	  	<SHOW> stringConstant() #PROMPT_SHOW(1)
	 |  <ADD> stringConstant() #PROMPT_ADD(1)
	) <SEMICOLON>}

SimpleNode compoundStatement() #COMPOUND : {}
{
    <LEFT_BRACE>
    (statement())*
    <RIGHT_BRACE>
    { return jjtThis; }
}

void whileLoop() #WHILE : {}
{
  <WHILE> <LEFT_PAREN> expression() <RIGHT_PAREN>
  (
	  compoundStatement() 
	| statement()
  )
}

void forLoop() #FOR : {}
{
  <FOR>
  <LEFT_PAREN>
  (assignment())?
  <SEMICOLON>
  (expression())?
  <SEMICOLON>
  (assignment())?
  <RIGHT_PAREN>
  (
	  compoundStatement()
	| statement()
  )
}

void doLoop() #DO : {}
{
	<DO>
	(
	  compoundStatement()
	| statement()
    )
    <WHILE> <LEFT_PAREN> expression() <RIGHT_PAREN> <SEMICOLON>
}

void ifStatement() #IF : {}
{
	<IF> <LEFT_PAREN> expression() <RIGHT_PAREN>
	(
	  compoundStatement()
	| statement()
    )
    ( LOOKAHEAD(1) <ELSE> (
		  compoundStatement()
		| statement()
    ))?
}

void declarationStatement() #void : {ArrayList<SymTabEntry> variableList;
	                                 int index = 0;}
{	<DOLLAR> <IDENTIFIER> {
	  		if (symTabStack.getCurrentNestingLevel() != 1)	  		{	  		  index = (int) symTabStack.getLocalSymTab().getReference().getAttribute(ROUTINE_LOCALS_COUNT);
	  		}
			SymTabEntry typeId = symTabStack.lookup(token.image.toLowerCase());
            typeId.appendLineNumber(token.beginLine);
            TypeSpec type = typeId.getTypeSpec();
	  
        }
        <IDENTIFIER> {
            SymTabEntry entry = symTabStack.getLocalSymTab().lookup(token.image.toLowerCase());
			if (entry != null)			{
				handleError(token);
				System.out.println("*** Variable already exists");
				return;
			}
            variableList = new ArrayList<SymTabEntry>();
            processVariableDecl(token, index++, variableList);
        }
        (
            <COMMA>
            <IDENTIFIER> {
                processVariableDecl(token, index++, variableList);
            }
        )*
        {
       		for (SymTabEntry variableId : variableList) {
           		variableId.setTypeSpec(type);
            }
        }
    <SEMICOLON>
}

void printStatement() #PRINT : {}
{	  <PRINT> simpleExpression() <SEMICOLON>}
void printlnStatement() #PRINTLN : {}
{
	  <PRINTLN> simpleExpression() <SEMICOLON>
}

void assignmentStatement() #void : {}
{
    assignment()
    <SEMICOLON>
}

void assignment() #ASSIGN : {SimpleNode exp;}
{	variable() {
        SymTabEntry entry = symTabStack.lookup(token.image);
        entry.appendLineNumber(token.beginLine);
        TypeSpec type = entry.getTypeSpec();
		if (type.getForm() == TypeFormImpl.PROCEDURE)
        {
    		entry.setDefinition(DefinitionImpl.PROCEDURE);
    		entry.setAttribute(ROUTINE_SYMTAB, symTabStack.push(entry));
 		}
    }
    <EQUALS> (
    	simpleExpression()
      | routine(entry) {jjtree.popNode();}
    )
}
void routineCall() #CALL : {}
{	variable() {
	  	SymTabEntry entry = symTabStack.lookup(token.image);
        entry.appendLineNumber(token.beginLine);
        TypeSpec type = entry.getTypeSpec();
		if (type.getForm() != TypeFormImpl.PROCEDURE)
        {
			//error
 		}
	}
	routineParameters()
}

void routine(SymTabEntry entry) #void : {SimpleNode rootNode;}
{
  <NEW> (parameterList(entry))? rootNode = compoundStatement()  {
    ICode iCode = ICodeFactory.createICode();
    iCode.setRoot(rootNode);
    entry.setAttribute(ROUTINE_CODE, rootNode);	entry.setAttribute(ROUTINE_ICODE, iCode);
	entry.setDefinition(DefinitionImpl.PROCEDURE);
	symTabStack.pop();
  }
}

void routineParameters() #PARAMETERS : {}
{
  <LEFT_PAREN> (
		  (expression() | reference())
		(
		  <COMMA> (expression() | reference())
		)*
	)? <RIGHT_PAREN> <SEMICOLON>
}

void parameterList(SymTabEntry entry) #void : {
						 ArrayList<SymTabEntry> variableList;
                         int index = 0;
                         TypeSpec type;
                         SymTabEntry typeId;
                         boolean reference;}{
	 {
	    variableList = new ArrayList<SymTabEntry>();
	 }    <LEFT_PAREN> (
		( <DOLLAR> { reference = false; }
		| <AT> { reference = true; })
		<IDENTIFIER>		{
		  	typeId = symTabStack.lookup(token.image.toLowerCase());
            typeId.appendLineNumber(token.beginLine);
            type = typeId.getTypeSpec();
		}
	(	  <IDENTIFIER>		{
		    if (reference)
		    	processReferenceDecl(token, index++, variableList);
		    else
            	processVariableDecl(token, index++, variableList);
       		variableList.get(index - 1).setTypeSpec(type);
		}
	)*
	(
	   <COMMA>
	|  <RIGHT_PAREN> {
	    entry.setAttribute(ROUTINE_PARMS, variableList);
	    entry.setAttribute(ROUTINE_LOCALS_COUNT, variableList.size());
	    return;}
 	)
  )*}

void reference() #REFERENCE : {}{	<AT> variable() 
} 

void expression() #void : {}
{
  simpleExpression() (
		<EE> simpleExpression() #EE(2)
      | <NE> simpleExpression() #NE(2)
      | <LT> simpleExpression() #LT(2)
      | <LE> simpleExpression() #LE(2)
      | <GT> simpleExpression() #GT(2)
      | <GE> simpleExpression() #GE(2)
	)?
}

void simpleExpression() #void : {}
{
  (term()
  | "-" term() #NEGATE(1))
  ("+" term() #ADD(2)
  | "-" term() #SUBTRACT(2))*
}

void term() #void : {}
{
    factor() (
        "*" factor() #MULTIPLY(2) 
      | "/" factor() #DIVIDE(2)
      | "%" factor() #MOD(2)
    )*
}

void factor() #void : {}
{
    variable()
  | integerConstant()
  | realConstant()
  | stringConstant()
  | "(" simpleExpression() ")"
}

void variable() #VARIABLE : {}
{
    <IDENTIFIER> {
        SymTabEntry variableId = symTabStack.lookup(token.image);
        variableId.appendLineNumber(token.beginLine);
        TypeSpec type = variableId.getTypeSpec();
        jjtThis.setTypeSpec(type);
        jjtThis.setAttribute(ID, token.image);
    }
}

void integerConstant() #INTEGER_CONSTANT : {}
{
    <INTEGER> {
        jjtThis.setTypeSpec(Predefined.integerType);
        jjtThis.setAttribute(VALUE, Integer.parseInt(token.image));
	    jjtThis.setAttribute(LINE, token.beginLine);
    }
}

void stringConstant() #STRING_CONSTANT : {String result;}
{	<STRING_LIT> {
	    result = token.image.substring(1, token.image.length() - 1);
	    jjtThis.setAttribute(LINE, token.beginLine);		jjtThis.setTypeSpec(Predefined.stringType);
		jjtThis.setAttribute(VALUE, result);
	}
}

void realConstant() #REAL_CONSTANT : {}
{
    <REAL> {
        jjtThis.setTypeSpec(Predefined.realType);
        jjtThis.setAttribute(VALUE, Float.parseFloat(token.image));
	    jjtThis.setAttribute(LINE, token.beginLine);
    }
}

void Error() #void : {}
{
    <ERROR>
}

JAVACODE
void processVariableDecl(Token token, int index,
                         ArrayList<SymTabEntry> variableList) #void
{
    SymTabEntry variableId = symTabStack.enterLocal(token.image);
    variableId.setIndex(index);
    variableId.setDefinition(DefinitionImpl.VARIABLE);
    variableId.appendLineNumber(token.beginLine);
    variableList.add(variableId);
}

JAVACODE
void processReferenceDecl(Token token, int index,
                         ArrayList<SymTabEntry> variableList) #void
{
    SymTabEntry variableId = symTabStack.enterLocal(token.image);
    variableId.setIndex(index);
    variableId.setDefinition(DefinitionImpl.REFERENCE);
    variableId.appendLineNumber(token.beginLine);
    variableList.add(variableId);
}

JAVACODE
String handleError(Token token) #void
{
    System.out.println("*** ERROR: Line " + token.beginLine +
                     " after \"" + token.image + "\"");

    do {
    	token = getNextToken();
    } while (token.kind != SEMICOLON);

	//jjtree.popNode();
    return token.image;
}
